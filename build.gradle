import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id "org.openapi.generator" version "6.6.0"
    id 'org.liquibase.gradle' version '2.2.0'
}

group = 'com.rviewer.skeletons'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springdoc:springdoc-openapi-starter-common:2.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.1.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'org.mapstruct:mapstruct:1.4.0.Final'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.0.Final'
    // Testing
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test:3.5.6'
    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test:6.1.1'
    // Database
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql:0.8.13.RELEASE'
    testImplementation 'io.r2dbc:r2dbc-h2'
    testImplementation 'com.h2database:h2:2.1.214'
    // Liquibase
    implementation "org.liquibase:liquibase-core"
    // Liquibase Plugin
    liquibaseRuntime 'org.liquibase:liquibase-core:4.16.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.postgresql:postgresql'
}

liquibase {
    activities {
        main {
            changeLogFile 'build/liquibase_change_log.yaml'
            url 'jdbc:postgresql://localhost:5432/postgres_rv_database'
            username 'rv_user'
            password 'rv_password'
            driver 'org.postgresql.Driver'
        }
    }
}

tasks.register('buildServer', GenerateTask) {
    generatorName = "spring"
    inputSpec = "$rootDir/api/open-api.spec.yaml"
    apiPackage = "com.rviewer.skeletons.infrastructure.controller"
    modelPackage = "com.rviewer.skeletons.application.model"
    outputDir = "$buildDir/generated/sources".toString()
    modelNameSuffix = "Dto"
    configOptions = [
            interfaceOnly                 : "true",
            performBeanValidation         : "true",
            useSpringBoot3                : "true",
            reactive                      : "true",
            additionalModelTypeAnnotations: "@lombok.Generated"
    ]

}

tasks.compileJava.dependsOn(buildServer)

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/sources/src/main/java"
        }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
    }
    ignoreFailures = true
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
    reportsDirectory = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

/*
************************************************************************************
************************ Task to obtain a clear test report ************************
************************************************************************************
*/
tasks.withType(Test).configureEach {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED

        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        debug {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED

            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}